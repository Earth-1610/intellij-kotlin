buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    //https://mvnrepository.com/artifact/org.jetbrains.kotlin.jvm/org.jetbrains.kotlin.jvm.gradle.plugin
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'

    //https://mvnrepository.com/artifact/org.jetbrains.intellij/org.jetbrains.intellij.gradle.plugin
    id "org.jetbrains.intellij" version "0.4.10"

    id 'maven-publish'
}

group 'com.itangcent'
version '0.6.2-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'

//for publish
apply from: rootDir.canonicalPath + '/script/publish.gradle'
apply plugin: 'maven-publish'
apply plugin: 'signing'


intellij {
//    version idea_version
//    version '2018.2.1'
    version '2017.3.5'
    pluginName plugin_name
    updateSinceUntilBuild false
    downloadSources true
    sandboxDirectory "idea-sandbox"
    updateSinceUntilBuild false
    sameSinceUntilBuild false
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-reflect
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "com.google.inject:guice:4.2.2"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

//task makeJar(dependsOn: ['compileKotlin', 'javadoc'], type: Jar) {
task makeJar(dependsOn: ['compileKotlin'], type: Jar) {
    delete 'build/libs/intellij-kotlin.jar' //the path of generated jar
    appendix = "tm"
    baseName = "intellij-kotlin"
    version = "1.0.0"
    classifier = "release"
    //suffix
    extension = "jar"
    //the final Jar name, if not set, defaults to [baseName]-[appendix]-[version]-[classifier].[extension]
    archiveName = "intellij-kotlin.jar"
    //paths where the resource to be packaged
    def srcClassesDir = [project.buildDir.absolutePath + "/classes/kotlin/main"] //The path of kotlin classes
    //original resource
    from srcClassesDir
    //include class only
    include "**/*.class"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Intellij Kotlin'
                description = 'Help for developing plugins for JetBrains products.'
                url = 'https://github.com/Earth-1610/intellij-kotlin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tangcent'
                        name = 'Tangcent'
                        email = 'pentatengcent@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Earth-1610/intellij-kotlin'
                    developerConnection = 'scm:git:https://github.com/Earth-1610/intellij-kotlin'
                    url = 'https://github.com/Earth-1610/intellij-kotlin'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

